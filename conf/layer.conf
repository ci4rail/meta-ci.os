# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
            ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "meta-cios"
BBFILE_PATTERN_meta-cios = "^${LAYERDIR}/"

# Override toradex-torizon layer
BBFILE_PRIORITY_meta-cios = "100"

LAYERDEPENDS_meta-cios = "meta-toradex-torizon"
LAYERSERIES_COMPAT_meta-cios = "warrior zeus"

#
# Define what is printed into /etc/build
# Done by openembedded-core/meta/classes/image-buildinfo.bbclass
# 
# Compare to the original, BUILD_ENVIRONMENT has been added
#
IMAGE_BUILDINFO_VARS ?= "DISTRO DISTRO_VERSION BUILD_ENVIRONMENT"

# Identify the build environment (e.g. the build container version), using environment variable TORIZON_BUILDER_VERSION.
# will default to "None" if env var does not exist
BUILD_ENVIRONMENT := "${@d.getVar('BB_ORIGENV',False).getVar('TORIZON_BUILDER_VERSION', False)}"

# Identify the repo-manifest that ties the layer recipes together, using environment variable GitVersion_BranchVersion
# will default to "None" if env var does not exist 
# It is important to do here an immediate assignment (:=), otherwise bitbake complains "The metadata is not deterministic..."
MANIFEST_VERSION := "${@d.getVar('BB_ORIGENV',False).getVar('GitVersion_BranchVersion', False)}"

TDX_PRERELEASE = "-CI.OS.LMP-${MANIFEST_VERSION}"
TDX_BUILD = ""
